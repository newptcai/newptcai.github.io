title: Two 3-d cyclic integrals by Mathematica
date: 2020-04-12
layout: post
tags:  analysis, CAS
status: published

*This post is converted to Markdown from [this Mathematica notebook]({static}/notebook/2020-04-12-triangle.nb) thanks to the [M2MD](https://github.com/kubaPod/M2MD) package by
[Kuba Podkalicki](https://github.com/kubaPod).*

---

I came across a paper [Counting triangles in power-law uniform random
graphs](https://arxiv.org/abs/1812.04289v1)* *by *Pu Gao, Remco van der Hofstad, Angus Southwell,
Clara Stegehuis  *on ArXiv today. Then I spotted two integrals, equation (2.1) and (2.2). These two
are left as unevaluated integrals. But they don’t look too intimidating so I gave them a try and
voilà they have closed form as below:

![1wagzewezpxds]({static}/images/2020-04-12-triangle/1wagzewezpxds.png)

for $2\leq t\leq 3$.

There is probably a book somewhere that has these formulas. But here I will show you how to use Mathematica to get them.

## The first integral

The first one is actually quite easy. You can do it with directly in Mathematica 12.1 without any effort. Let

```mathematica
int = Inactive[Integrate][(x*y*z)^(2 - t)/((1 + x*y)*(1 + y*z)*
          (1 + x*z)), {x, 0, Infinity}, {y, 0, Infinity}, 
     {z, 0, Infinity}]
```

![1lvcxr0y375di]({static}/images/2020-04-12-triangle/1lvcxr0y375di.png)

With the assumption

```mathematica
assume = 2 < t < 3;
```

we can compute this integral symbolically 

```mathematica
intSym = Assuming[assume, ex[1] // Activate // FullSimplify]

(*ex[1]*)
```

To be sure let’s do some numeric verification.

```mathematica
intN = ex[1] /. {Integrate -> NIntegrate}

(*ex[1]*)
```

```mathematica
Table[1 - intN/intSym // Activate // Quiet, {t, 2, 11/4, 1/4}]

(*{0, 0, 0, 0}*)
```

As, you can see the relative differences between numeric and symbolic integral are quite small. This increases our confidence that we got the right answer.

## The second integral

Let 

```mathematica
f[x_, y_, 
  z_] := (x y z)^-t (1 - E^(-x y)) (1 - E^(-z y)) (1 - E^(-z x))
```

We want to compute

```mathematica
int2[1] = Inactive[Integrate][f[x, y, z], {x, 0, Infinity}, 
     {y, 0, Infinity}, {z, 0, Infinity}]
```

![1isrpmk5f06g1]({static}/images/2020-04-12-triangle/1isrpmk5f06g1.png)

Mathematica cannot do this directly. But we can do a change of variable by integrating $f(g(a,b,c))$ with

```mathematica
g[a_, b_, c_] := {(Sqrt[a]*Sqrt[c])/Sqrt[b], 
     (Sqrt[a]*Sqrt[b])/Sqrt[c], (Sqrt[b]*Sqrt[c])/Sqrt[a]}
```

In other words 

```mathematica
HoldForm[g[a, b, c]] == g[a, b, c]
```

![0zopnnmwj7eeo]({static}/images/2020-04-12-triangle/0zopnnmwj7eeo.png)

We can use the chain rule. First let’s get the Jacobian.

```mathematica
jacob = D[g[a, b, c], {{a, b, c}}] // Simplify // Det
```

![0op60u4mzxejt]({static}/images/2020-04-12-triangle/0op60u4mzxejt.png)

So we can instead integrate.

```mathematica
int2[2] = 
 int2[1] /. f[x, y, z] -> (f @@ g[a, b, c])*jacob /. {x -> a, y -> b, 
   z -> c}
```

![04ul3copum1p7]({static}/images/2020-04-12-triangle/04ul3copum1p7.png)

This Mathematica knows how to do

```mathematica
int2Sym = Assuming[assume, int2[2] // Activate // FullSimplify]
```

![1r7ga4931ftg4]({static}/images/2020-04-12-triangle/1r7ga4931ftg4.png)

Again let’s check numerically. This time it’s a bit bit challenging and you have to play a bit with the strategy of the integration.

Before change of variable.

```mathematica
int2N[1] = Inactive[NIntegrate][f[x, y, z], {x, 0, Infinity}, 
      {y, 0, Infinity}, {z, 0, Infinity}]
```

![1d6ysd0acvxnp]({static}/images/2020-04-12-triangle/1d6ysd0acvxnp.png)

After change of variable.

```mathematica
int2N[2] = Inactive[NIntegrate][f @@ g[a, b, c]*jacob, 
      {a, 0, Infinity}, {b, 0, Infinity}, {c, 0, Infinity}]
```

![1cvsb75b9nx34]({static}/images/2020-04-12-triangle/1cvsb75b9nx34.png)

The differences comparing to the closed form are

```mathematica
Table[1 - {int2N[1], int2N[2]}/int2Sym /. t -> 2 // Activate // 
  Quiet, {t, 2, 11/4, 1/4}]

(*{{1.20381*10^-7, 2.77161*10^-8}, {2.35678*10^-6, 
  2.21698*10^-6}, {0.000204701, 0.000260132}, {0.0251801, 0.0283442}}*)
```

So again, we can be quite sure that we  got the right answer.

## Reference

Gao, P., van der Hofstad, R., Southwell, A., & Stegehuis, C.(2018).Counting triangles in power -- law
uniform random graphs. [ArXiv:1812.04289](http://arxiv.org/abs/1812.04289) [Math]. 
